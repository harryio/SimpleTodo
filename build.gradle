// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def properties = loadProperties()

ext {
    keyAlias = properties.getProperty("KEY_ALIAS")
    keyPassword = properties.getProperty("KEY_PASSWORD")
    storePassword = properties.getProperty("STORE_PASSWORD")
    fabricApiKey = properties.getProperty("FABRIC_API_KEY")
}

def loadProperties() {
    if (project.rootProject.file('local.properties').exists()) {
        return loadFromLocalProperties()
    } else {
        return loadFromEnvironment()
    }
}

def loadFromLocalProperties() {
    Properties localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
    return localProperties
}

def loadFromEnvironment() {
    Properties envProperties = new Properties()
    envProperties.setProperty('KEY_ALIAS', System.getenv("KEY_ALIAS"))
    envProperties.setProperty('KEY_PASSWORD', System.getenv("KEY_PASSWORD"))
    envProperties.setProperty('STORE_PASSWORD', System.getenv("STORE_PASSWORD"))
    envProperties.setProperty('FABRIC_API_KEY', System.getenv("FABRIC_API_KEY"))
    return envProperties
}